"""
Copyright (c) 2008, Kalisko Project Leaders
All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer
      in the documentation and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
"""
Import('module')

if not module.GetOption('clean'):
	conf = Configure(module)
	
	if conf.env['PLATFORM'] == 'posix':
		conf.env.ParseConfig('python-config --cflags')
		if not conf.CheckLib('python2.6'):
				print('Error: Could not find library python2.6!')
				Exit(1)
	elif conf.env['PLATFORM'] == 'win32':
		import _winreg
		import os
		try:
			pyRegistryInstallPath = _winreg.OpenKey(_winreg.HKEY_LOCAL_MACHINE, r"SOFTWARE\Python\PythonCore\2.6\InstallPath")
			pyInstallPath = _winreg.QueryValue(pyRegistryInstallPath, "") # Gets the "(Default)" value from registry
			conf.env.Append(CPPPATH = [os.path.join(pyInstallPath, 'include')])
			conf.env.Append(LIBPATH = [os.path.join(pyInstallPath, 'libs')])
		except Exception, (error):
			print('Error: Could not find Python\'s installation path. Error: %s' % (error))
			Exit(1)
			
		# Please keep in mind that the lib we use under Windows has a diffrent
		# name as for other platforms.
		if not conf.CheckLib('python26'):
			print('Error: Could not find library python2.6!')
			Exit(1)
			
		conf.env.Append(LIBS = ['python26'])
			
	module = conf.Finish()

module.SharedLibrary('../lang_python', Glob('*.c'))